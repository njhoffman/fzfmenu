#!/bin/zsh
# git checkout-branch selector
forgit::checkout::branch() {
    forgit::inside_work_tree || return 1
    [[ $# -ne 0 ]] && { git checkout -b "$@"; return $?; }
    local cmd preview opts branch
    cmd="git branch --color=always --verbose --all | sort -k1.1,1.1 -r"
    preview="git log {1} --graph --pretty=format:'$forgit_log_format' --color=always --abbrev-commit --date=relative"
    opts="
        $FORGIT_FZF_DEFAULT_OPTS
        +s +m --tiebreak=index --header-lines=1
        $FORGIT_CHECKOUT_BRANCH_FZF_OPTS
        "
    branch="$(eval "$cmd" | FZF_DEFAULT_OPTS="$opts" fzf --preview="$preview" | awk '{print $1}')"
    [[ -z "$branch" ]] && return 1
    # track the remote branch if possible
    if ! git checkout --track "$branch" 2>/dev/null; then
        git checkout "$branch"
    fi
}
